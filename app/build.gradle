apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

/*
 * Enable the application to consume the Video Android SDK directly from source
 * instead of from Bintray.
 *
 * The property is not defined by default when consuming the
 * application as a stand-alone project, but is defined by default in the Video Android SDK project.
 * This property enables the Video Android SDK project to build the application as a git subtree
 * with the contents of the source itself as a dependency. This coupling is intentional as it allows
 * SDK developers to iterate on SDK features within the application that is maintained in a separate
 * project.
 */
def videoAndroidSdkDepProp = "videoAppSdkDependency"
def videoAndroidSdkDep = (project.hasProperty(videoAndroidSdkDepProp) ?
        project("${project.property(videoAndroidSdkDepProp)}") :
        "com.twilio:video-android:5.4.0")
def versionCodeProperty = project.property('versionCode') as Integer

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    lintOptions {
        disable 'GradleDependency'
        warningsAsErrors true
        // TODO: Fix all baseline app warnings and errors GSDK-1984
        baseline file("lint-baseline.xml")
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    defaultConfig {
        applicationId "com.twilio.video.app"

        minSdkVersion 16
        targetSdkVersion 28

        versionName "0.1.0"
        versionCode versionCodeProperty

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        testInstrumentationRunner "com.twilio.video.app.InstrumentationRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
        debug {
            storeFile file('video-android-app.keystore')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "environment"

    productFlavors {
        internal {
            dimension "environment"
            applicationId "com.twilio.video.app.internal"
            buildConfigField 'String', 'ENVIRONMENT_DEFAULT', '"production"'
            buildConfigField 'String', 'APPCENTER_APP_KEY',
                    "\"${getLocalProperty("APPCENTER_APP_KEY")}\""
        }

        twilio {
            dimension "environment"
            buildConfigField 'String', 'ENVIRONMENT_DEFAULT', '"production"'
        }

        community {
            dimension "environment"
            applicationId "com.twilio.video.app.community"
            buildConfigField 'String', 'ENVIRONMENT_DEFAULT', '"production"'
            buildConfigField("String",
                    "TWILIO_ACCESS_TOKEN",
                    "\"${getLocalProperty("TWILIO_ACCESS_TOKEN")}\"")
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        // Ignore twilio debug and community release builds
        if ((names.contains("twilio") && variant.buildType.name == "debug") ||
                (names.contains("community") && variant.buildType.name == "release")) {
            setIgnore(true)
        }
    }
}

dependencies {
    def butterknifeVersion = '10.2.0'
    def daggerVersion = '2.25.4'
    def daggerAndroidProcessor = "com.google.dagger:dagger-android-processor:$daggerVersion"
    def daggerCompiler = "com.google.dagger:dagger-compiler:$daggerVersion"
    def retrofitVersion = '2.6.4'
    def espressoVersion = '3.2.0'
    def espresso = "androidx.test.espresso:espresso-core:$espressoVersion"
    def androidXTest = '1.2.0'
    def testCore = "androidx.test:core:$androidXTest"
    def junitExtensions = 'androidx.test.ext:junit-ktx:1.1.1'
    def lifecycleVersion = '2.2.0'
    def coroutinesAndroidVersion = '1.3.3'

    implementation 'com.facebook.conceal:conceal:2.0.2@aar'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesAndroidVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesAndroidVersion"
    // TODO Remove as part of https://issues.corp.twilio.com/browse/AHOYAPPS-445
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$coroutinesAndroidVersion"
    implementation 'com.twilio:twilio-android-env:1.0.0'
    implementation videoAndroidSdkDep
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.fragment:fragment:1.2.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.appyvet:materialrangebar:1.3"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "com.google.firebase:firebase-core:17.2.2"
    implementation 'com.google.firebase:firebase-auth:19.2.0'
    implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation project(':audioswitch')

    internalImplementation "com.microsoft.appcenter:appcenter-distribute:2.5.1"

    kapt daggerAndroidProcessor
    kapt daggerCompiler
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    testImplementation testCore
    testImplementation espresso
    testImplementation junitExtensions
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesAndroidVersion"
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    kaptTest daggerAndroidProcessor
    kaptTest daggerCompiler

    androidTestImplementation testCore
    androidTestImplementation espresso
    androidTestImplementation junitExtensions
    androidTestImplementation "androidx.test:runner:$androidXTest"
    androidTestImplementation "androidx.test:rules:$androidXTest"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.9.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

def getLocalProperty(key) {
    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def value = properties.getProperty(key)
        return value
    } else {
        logger.log(LogLevel.WARN, "Could not find local.properties in " +
                project.rootProject.toString() + ". Community variant cannot connect to room" +
                " without credentials")
    }
}

apply plugin: 'com.google.gms.google-services'
